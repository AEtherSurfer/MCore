apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

ext {
	buildNumber = "DEV"
}

group = "com.massivecraft"
version = "2.8.6AES-SNAPSHOT"

repositories {
	mavenLocal()
    mavenCentral()
	maven {
		//Bukkit Repo
		url "http://repo.bukkit.org/content/groups/public"
    }
	maven {
		//Spigot Repo
		url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
	}
	maven {
		//Vault Repo
		url "http://nexus.theyeticave.net/content/repositories/pub_releases"
    }
	maven {
		//Required for upline dependencies
		url "http://omwah.sdf.org/maven/repo/snapshots/"
    }
	maven {
		//Required for upline dependencies
		url "http://repo.greatmancode.com/content/groups/public/"
    }
}

configurations {
	provided
}

dependencies {
	provided ('org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT') {
		exclude module: 'bungeecord-chat'
	}
	provided ('net.milkbowl.vault:VaultAPI:1.5') {
		exclude module: 'Multiverse-Inventories'
		exclude module: 'bukkit'
	}
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir "$projectDir"
			include 'plugin.yml'
		}
		compileClasspath += configurations.provided
	}
}

processResources  {
	filter {
		line ->
		line
		.replace('${name}', project.name)
		.replace('${version}', project.version)
		.replace('${buildNumber}', project.ext.buildNumber)
		.replace('${pluginMainClass}', "${project.group}.${project.name.toLowerCase()}.$project.name")
	}
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.11'
}
